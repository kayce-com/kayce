// SPDX-License-Identifier: AGPL-3.0
pragma solidity ^0.8.4;

import { ERC20 } from "solmate/tokens/ERC20.sol";

/// @author zefram.eth
/// @title Multicall
/// @notice Enables calling multiple methods in a single call to the contract as well as
///         functionality to call permit on any EIP-2612-compliant token for use in the route


abstract contract Multicall {
    function multicall(bytes[] calldata data)
        external
        payable
        returns (bytes[] memory results)
    {
        results = new bytes[](data.length);
        for (uint256 i = 0; i < data.length; i++) {
            (bool success, bytes memory result) = address(this).delegatecall(
                data[i]
            );

            if (!success) {
                // Next 5 lines from https://ethereum.stackexchange.com/a/83577
                if (result.length < 68) revert();
                assembly {
                    result := add(result, 0x04)
                }
                revert(abi.decode(result, (string)));
            }

            results[i] = result;
        }
    }

    /// Enabling self permit functionality ------------------
    /// @dev These functions are expected to be embedded in multicalls to allow EOAs to 
    ///      approve a contract and call a function that requires an approval in a single transaction.

    function selfPermit(
        ERC20 token,
        uint256 value,
        uint256 deadline,
        uint8 v,
        bytes32 r,
        bytes32 s
    ) public payable {
        token.permit(msg.sender, address(this), value, deadline, v, r, s);
    }

    function selfPermitIfNecessary(
        ERC20 token,
        uint256 value,
        uint256 deadline,
        uint8 v,
        bytes32 r,
        bytes32 s
    ) external payable {
        if (token.allowance(msg.sender, address(this)) < value)
            selfPermit(token, value, deadline, v, r, s);
    }
}
